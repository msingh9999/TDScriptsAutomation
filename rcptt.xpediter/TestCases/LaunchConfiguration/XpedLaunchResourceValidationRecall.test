--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _eEaF4ER8EeamKrg04Gqe-Q,_ktRQQER8EeamKrg04Gqe-Q,_PprFMER9EeamKrg04Gqe-Q,_l8_BIER7EeamKrg04Gqe-Q,_kRuZMER7EeamKrg04Gqe-Q,_aEjwMLjIEeebSNbxTgM6gQ
Element-Name: XpedLaunchResourceValidationRecall
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _H8bgIONDEea3ark9aBxh3g
Runtime-Version: 2.2.0.201706152316
Save-Time: 10/25/17 6:45 AM
Tags: CWE-107326
Testcase-Type: ecl

------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa
Content-Type: text/plain
Entry-Name: .description

CWE-107326 - Test the validation of migrated datasets and the ability to recall them from the 'Resource Validation Failed' dialog
------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa--
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

log "---------------------------------------------- XpedLaunchResourceValidationRecall ----------------------------------------------"

global [val jclDataset "XDEVREG.XPED.JCL"]
global [val jclMember "XDVALRCL"]
global [val suffix "validate recall"]
global [val jobName "XDXPRCLS"]
global [val stepName "MEMDISP"]
global [val migratedDDIO "XDEVREG.XPED.VALIDATE.RECALL.DDIO"]
global [val loadLib "XDEVREG.XPED.VALIDATE.RECALL.LOADLIBE"]

// Migrate some assets used in the launch configuration
add-filter
submit-job "XDEVREG.HS.JCL" "MIGRAT10" -jobName "XDVMIG10"

get-menu "Run/Debug Configurations..." | click
with [get-window "Debug Configurations"] 
{
    get-tree | select "Xpediter Batch Debug Session"
    get-button "New launch configuration" | click
    get-editbox -after [get-label "Name:"] | set-text [format "%s %s" "Batch" $suffix]
    with [get-group "Job to debug" | get-editbox -after [get-label "Dataset(member):"]] 
    {
        set-text [format "%S(%S)" $jclDataset $jclMember]
    }
    with [get-group "Debug criteria"] 
    {
        with [get-editbox -after [get-label "Job name:"]] 
        {
            set-text $jobName
        }
        get-button "Add..." | click
    }
    with [get-window Add] 
    {
        get-editbox -after [get-label "Step:"] | set-text $stepName
        with [get-editbox -after [get-label "Program:"]] 
        {
            set-text $stepName
        }
        get-button OK | click
    }
    get-tab-folder | get-tab-item DDIO | click
    get-group "DDIO (Compuware source listing files)" | get-button "Add..." | click
    with [get-window Datasets] 
    {
        get-editbox | set-text $migratedDDIO
        get-button OK | click
    }
    get-tab-folder | get-tab-item "Load Libraries" | click
    get-group "Load libraries" | get-button "Add..." | click
    with [get-window Datasets] 
    {
        get-editbox | set-text $loadLib
        get-button OK | click
    }
    
    get-button Apply | click
    get-button Debug | click
}

handle-login-dialog

with [get-window "Resource Validation Failed"] 
{
	with [get-table]
	{
		get-item -path "XDEVREG.XPED.VALIDATE.RECALL.DDIO" | get-property "columns[1]" | equals Migrated | verify-true
		get-item -path "XDEVREG.XPED.VALIDATE.RECALL.LOADLIBE" | get-property "columns[1]" | equals Migrated | verify-true
	}
	get-button "Recall All" | click
}


try -times 3 -delay 300 -command
{
	with [get-window "Resource Validation Failed" | get-table] 
	{
	    get-item -path "XDEVREG.XPED.VALIDATE.RECALL.DDIO" | get-property "columns[1]" | equals "Recalled" | verify-true
	    get-item -path "XDEVREG.XPED.VALIDATE.RECALL.LOADLIBE" | get-property "columns[1]" | equals "Recalled" | verify-true
	}
}
-catch
{
	log "Failed waiting for recall of resources!"
}	



get-window "Resource Validation Failed" | get-button Cancel | click

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
