--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: ProceduresConsoles
Element-Type: context
Element-Version: 2.0
Id: _Qgyi4EULEeamKrg04Gqe-Q
Runtime-Version: 2.0.1.201508250612
Save-Time: 7/8/16 8:56 AM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

/* Close all the open consoles.  Default will attempt to close 5 console windows. */
proc "close-all-consoles" [val consoleCount 5]
{
	if [has-view Console]
	{
		with [get-view Console]
		{
			loop [val count 0]
			{
				if [$count | lt $consoleCount]
				{
					if [button-enabled $DisplaySelectedConsole]
					{
						if [button-enabled $TSOCancelButton]
						{
							get-button $TSOCancelButton | click
						}
					
						if [has-button $CloseConsole]
						{
							get-button $CloseConsole | click
						}
						-else
						{
							get-button $DisplaySelectedConsole | click
						}
					
						recur [$count | plus 1]
					}
				}
			}
		}
	}
}

proc "find-console-menu-item" [val itemNumber] [val menuItem]
{
    try -command
    {
        log [format "%s %s" [$itemNumber | str] $menuItem]
        get-menu [format "%s %s" [$itemNumber | str] $menuItem] 
        bool true
    } -catch
    {
        bool false
    }
}

proc "get-new-console" [val menuItem]
{
    loop [val count 1] 
    {
        if [find-console-menu-item -itemNumber $count -menuItem $menuItem] 
        {
           log [format "Found Console at %s" [$count | str]]
           get-menu [format "%s %s" [$count | str] $menuItem] | click
        } -else 
        {
            if [$count | lt 10]
            {
               recur [$count | plus 1] 
            }
        }
    } 
}

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--
