--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _kRuZMER7EeamKrg04Gqe-Q
Element-Name: PDSProperties
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _Y4MuEL4-EeajusfmOW4rkQ
Runtime-Version: 2.2.0.201606232320
Save-Time: 8/7/17 12:53 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

log "---------------------------------------------- PDSProperties ----------------------------------------------"

global [val targetPDS [format "XDEVREG.PDSPROP.T%S" [get-time | format-time -format mmssSSS]]]
global [val sourcePDS "XDEVREG.COBOL"]
global [val testMember "OLPDLT1"]
global [val filterXdevregPdsprop "XDEVREG.PDSPROP"]

add-filter $FILTER_XDEVREG

// Copy XDEVREG.COBOL to a temporary PDS that we can delete a member and the entire PDS
copy-pds -hostName $HOST -portNum $PORT -filter $FILTER_XDEVREG -sourcePDS $sourcePDS -targetPDS $targetPDS

remove-filter -filter $FILTER_XDEVREG
add-filter -filter $filterXdevregPdsprop

// Validate properties view table is showing for PDS
get-view $HOST_EXPLORER | get-tree | select [get-pds-path -hostName $HOST -portNUM $PORT -filter $filterXdevregPdsprop -dataset $targetPDS]
    | get-menu Properties | click

verify-properties-view-caption -captionValue $targetPDS | verify-true
with [get-view Properties | get-tree] {
    get-item Name | get-property caption | equals Name | verify-true
    get-item Name | get-property "getData().getValueAsString()" | equals $targetPDS | verify-true
}

// Verify properties view table is showing for PDS member
get-view $HOST_EXPLORER | get-tree 
    | select [get-pds-member-path -hostName $HOST -portNUM $PORT -filter $filterXdevregPdsprop -dataset $targetPDS -member $testMember] | get-menu Properties | click

verify-properties-view-caption -captionValue $testMember | verify-true
with [get-view Properties | get-tree] {
    get-item Name | get-property caption | equals Name | verify-true
    get-item Name | get-property "getData().getValueAsString()" | equals $testMember| verify-true
}

// Delete PDS member should clear properties view
get-view $HOST_EXPLORER | get-tree 
    | select [get-pds-member-path -hostName $HOST -portNUM $PORT -filter $filterXdevregPdsprop -dataset $targetPDS -member $testMember] | get-menu Delete | click
with [get-window Delete] {
    get-button Delete | click
    get-button Done | click
}

verify-properties-view-caption -captionValue $testMember | verify-false

// Deleting entire PDS should also clear properties view
with [get-view $HOST_EXPLORER | get-tree] {
    select [get-pds-path -hostName $HOST -portNUM $PORT -filter $filterXdevregPdsprop -dataset $targetPDS] | get-menu Properties | click
    select [get-pds-path -hostName $HOST -portNUM $PORT -filter $filterXdevregPdsprop -dataset $targetPDS]  | get-menu Delete | click
}
get-window Delete | get-button Delete | click
get-window Delete | get-table | get-item $targetPDS | get-property "columns[2]" 
    | equals [format "Resource %S successfully deleted." $targetPDS] | verify-true
get-window Delete | get-button Done | click

verify-properties-view-caption -captionValue $targetPDS | verify-false

remove-filter -filter $filterXdevregPdsprop
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
