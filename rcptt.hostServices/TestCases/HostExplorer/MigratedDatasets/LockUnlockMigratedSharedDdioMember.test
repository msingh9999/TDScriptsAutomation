--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _kRuZMER7EeamKrg04Gqe-Q,_PprFMER9EeamKrg04Gqe-Q
Element-Name: LockUnlockMigratedSharedDdioMember
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _9YEisLZiEeaI5LAUP6iy4A
Runtime-Version: 2.2.0.201704062317
Save-Time: 7/10/17 11:44 AM
Testcase-Type: ecl

------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa
Content-Type: text/plain
Entry-Name: .description

Test that Lock/Unlock actions display dataset name in migrated error message when shared dataset is migrated.

------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa--
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

global [val SHARED_DIR_DDIO_NAME "XDEVREG.SSD.TEST"]
global [val SHARED_DIR_DB_NAME "XDEVREG.SSD.TEST.DB"]
global [val DDIO_MEMBER_NAME "QALVARS \\(1\\)"]
global [val DDIO_MEMBER_NAME2 "QALVARS2 \\(2\\)"]
add-filter

// Make sure member 1 is unlocked (we don't expect this member to be locked, but this is in case the test failed
// and actually locked the member):
with [get-view "Host Explorer" | get-tree | select-pds-member $SHARED_DIR_DDIO_NAME $DDIO_MEMBER_NAME]
{
	if [has-menu Unlock]
	{
		recall-dataset $SHARED_DIR_DB_NAME
		// We have to reselect the pds member because the above recall changes the selection.
		select-pds-member $SHARED_DIR_DDIO_NAME $DDIO_MEMBER_NAME
		get-menu Unlock | click
		with [get-window Unlock]
		{
			get-button Unlock | click
			get-button Done | click
		}

		// backup dataset so migrate will work:
		submit-job "XDEVREG.HS.JCL" "BACKDS9" -jobName "XDEVBAC9"
	}
}

// Make sure member 2 is locked (we don't expect this member to be unlocked, but this is in case the test failed
// and actually unlocked the member):
with [get-view "Host Explorer" | get-tree | select-pds-member $SHARED_DIR_DDIO_NAME $DDIO_MEMBER_NAME2]
{
	if [has-menu Lock]
	{
		recall-dataset $SHARED_DIR_DB_NAME
		// We have to reselect the pds member because the above recall changes the selection.
		select-pds-member $SHARED_DIR_DDIO_NAME $DDIO_MEMBER_NAME2
		get-menu Lock | click
		with [get-window Lock]
		{
			get-button Lock | click
			get-button Done | click
		}

		// backup dataset so migrate will work:
		submit-job "XDEVREG.HS.JCL" "BACKDS9" -jobName "XDEVBAC9"
	}
}

// Make sure the shared dataset is migrated:
select-pds $SHARED_DIR_DB_NAME | get-menu "Refresh" | click
if [get-dataset-node $SHARED_DIR_DB_NAME | has-menu "Recall" | not]
{
	submit-job -dataset "XDEVREG.HS.JCL" -member "MIGRATE9" -jobName "XDEVMIG9"
}

/*
 * Verify DDIO Lock action display dataset name in migrated error message.
 */
get-view "Host Explorer" | get-tree | select [get-pds-member-path $SHARED_DIR_DDIO_NAME $DDIO_MEMBER_NAME] 
    | get-menu Lock | click

with [get-window Lock]
{
	get-button Lock | click

	with [get-table | get-item $DDIO_MEMBER_NAME]
	{
	    get-property "columns[0]" | equals $DDIO_MEMBER_NAME | verify-true
	    get-property "columns[1]" -raw | matches [format ".*dataset %s is migrated.*" $SHARED_DIR_DB_NAME]
	    	| verify-true
	}

	get-button Done | click
}

/**
 * Verify dataset does not show as migrated (since this is a shared dataset).
 */
get-view "Host Explorer" | get-tree | get-item [get-pds-path $SHARED_DIR_DDIO_NAME] | get-property "image.path"
	| equals "com.compuware.frameworks.hostservices.hostexplorer/icons/DDIODataset.gif" | verify-true

/*
 * Verify DDIO Unlock action display dataset name in migrated error message.
 */
get-view "Host Explorer" | get-tree | select [get-pds-member-path $SHARED_DIR_DDIO_NAME $DDIO_MEMBER_NAME2] 
    | get-menu Unlock | click

with [get-window Unlock]
{
	get-button Unlock | click

	with [get-table | get-item $DDIO_MEMBER_NAME2]
	{
	    get-property "columns[0]" | equals $DDIO_MEMBER_NAME2 | verify-true
	    get-property "columns[1]" -raw | matches [format ".*dataset %s is migrated.*" $SHARED_DIR_DB_NAME]
	    	| verify-true
	}

	get-button Done | click
}

/**
 * Verify dataset does not show as migrated (since this is a shared dataset).
 */
get-view "Host Explorer" | get-tree | get-item [get-pds-path $SHARED_DIR_DDIO_NAME] | get-property "image.path"
	| equals "com.compuware.frameworks.hostservices.hostexplorer/icons/DDIODataset.gif" | verify-true

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
