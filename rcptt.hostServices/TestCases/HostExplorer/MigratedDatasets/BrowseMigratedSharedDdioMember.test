--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _kRuZMER7EeamKrg04Gqe-Q,_PprFMER9EeamKrg04Gqe-Q
Element-Name: BrowseMigratedSharedDdioMember
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _rc35MMFqEeaZifM36uMTJA
Runtime-Version: 2.1.0.201606221726
Save-Time: 12/14/16 7:40 AM
Testcase-Type: ecl

------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa
Content-Type: text/plain
Entry-Name: .description

Tests that we get "Recall" dialog offering to recall the migrated dataset when browsing a DDIO member using a shared directory.
 * Answering "Yes" recalls the dataset
 * Answering "No" does not recall the dataset
------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa--
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

global [val SHARED_DIR_DDIO_NAME "XDEVREG.SSD.TEST"]
global [val SHARED_DIR_DB_NAME "XDEVREG.SSD.TEST.DB"]
global [val DDIO_MEMBER_NAME "QALVARS \\(1\\)"]
add-filter

// Make sure the shared dataset is migrated:
select-pds $SHARED_DIR_DB_NAME | get-menu "Refresh" | click
if [get-dataset-node $SHARED_DIR_DB_NAME | has-menu "Recall" | not]
{
	submit-job -dataset "XDEVREG.HS.JCL" -member "MIGRATE9" -jobName "XDEVMIG9"
}

/*
 * Verify we get "Recall" dialog offering to recall the migrated dataset when browsing a DDIO member using a shared directory.
 */
select-pds-member $SHARED_DIR_DDIO_NAME $DDIO_MEMBER_NAME  | get-menu "Browse" | click
with [get-window "Recall Dataset" ]
{
	with [get-label -index 1 | get-property caption]
	{
		matches [format ".*%s is migrated.*" $SHARED_DIR_DB_NAME]| verify-true
		matches ".*Would you like to recall.*?" | verify-true
	}
}

/*
 * Verify answering "Yes" recalls the dataset.
 */
get-window "Recall Dataset" | get-button Yes | click
select-pds $SHARED_DIR_DB_NAME | get-menu "Refresh" | click
get-dataset-node $SHARED_DIR_DB_NAME | get-property "image.path"
	| equals "com.compuware.frameworks.hostservices.hostexplorer/icons/DDIODataset.gif"  | verify-true

/*
 * Verify we still get "Recall" dialog offering to recall the migrated dataset when browsing a normal PDS member.
 */
submit-job -dataset "XDEVREG.HS.JCL" -member "MIGRATE9" -jobName "XDEVMIG9"
select-pds-member $SHARED_DIR_DDIO_NAME $DDIO_MEMBER_NAME | get-menu "Browse" | click
with [get-window "Recall Dataset"]
{
	with [get-label -index 1 | get-property caption]
	{
		matches [format ".*%s is migrated.*" $SHARED_DIR_DB_NAME]| verify-true
		matches ".*Would you like to recall.*?" | verify-true
	}
}

/*
 * Verify answering "No" does not recall the dataset.
 */
get-window "Recall Dataset" | get-button No | click
select-pds $SHARED_DIR_DB_NAME | get-menu "Refresh" | click
get-dataset-node $SHARED_DIR_DB_NAME | get-property "image.path"
	| equals "com.compuware.frameworks.imagerepository/images/obj/directory_migrated.gif" | verify-true


------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
