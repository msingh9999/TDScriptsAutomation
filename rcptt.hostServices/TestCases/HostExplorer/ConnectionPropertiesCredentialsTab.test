--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _kRuZMER7EeamKrg04Gqe-Q
Element-Name: ConnectionPropertiesCredentialsTab
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _U3SY8AWxEeeYiqdW2ECFig
Runtime-Version: 2.2.0.201606232320
Save-Time: 4/18/18 1:12 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

log "---------------------------------------------- ConnectionPropertiesCredentialsTab ----------------------------------------------"

// the context has already added and logged into the default host

// logout of the host and verify we don't have any saved credentials
get-view "Host Explorer" | get-tree | select [format "Hosts/cw01:%s" $PORT] | get-menu Logout | click
get-window Logout | get-button Yes | click
get-view "Host Explorer" | get-button "Host Explorer Preferences" | click
with [get-window "Preferences (Filtered)"] {
    get-table | select Ungrouped
    get-button "Edit..." | click
    get-window "Host Connections" | get-tab-folder | get-tab-item Credentials | click
}
get-window "Preferences (Filtered)" | get-window "Host Connections" | get-group "Saved Credentials" | get-table 
    | get-property itemCount | equals 0 | verify-true
with [get-window "Preferences (Filtered)"] {
    get-window "Host Connections" | get-button Cancel | click
    get-button OK | click
}

// log into the host, saving credentials
get-view "Host Explorer" | get-tree | select [format "Hosts/cw01:%s" $PORT] | get-menu "Login..." | click
with [get-window Login] {
    with [get-editbox -after [get-label "User ID:"]] {
        set-text xdevreg
        key-type "TRAVERSE_TAB_NEXT"
    }
    get-editbox -after [get-label "Password:"] | set-text [decrypt "GCCQ5KeLMRHd4spOzx8A1g=="]
    get-button "Save credentials" | check
    get-button OK | click
}

// handle the possibilty of the secure storage prompt
try -command {
	get-window "Secure Storage - Passworc Hint Needed" | get-button No | click
} -catch {
}

// verify that we have saved credentials now and that the button enablement is correct
get-view "Host Explorer" | get-button "Host Explorer Preferences" | click
with [get-window "Preferences (Filtered)"] {
    get-table | select Ungrouped
    get-button "Edit..." | click
    get-window "Host Connections" | get-tab-folder | get-tab-item Credentials | click
}
get-window "Preferences (Filtered)" | get-window "Host Connections" | get-group "Saved Credentials" | get-table 
    | get-property itemCount | equals 1 | verify-true
with [get-window "Preferences (Filtered)" | get-window "Host Connections" | get-group "Saved Credentials"] {
    get-button "Edit..." | get-property enablement | equals true | verify-false
    get-button Remove | get-property enablement | equals true | verify-false
    get-button "Remove All" | get-property enablement | equals true | verify-true
}
get-window "Preferences (Filtered)" | get-window "Host Connections" | get-group "Saved Credentials" | get-table 
    | select [format "hci:\\/\\/10.10.0.200:%s" $PORT]
with [get-window "Preferences (Filtered)" | get-window "Host Connections" | get-group "Saved Credentials"] {
    get-button "Edit..." | get-property enablement | equals true | verify-true
    get-button Remove | get-property enablement | equals true | verify-true
    get-button "Remove All" | get-property enablement | equals true | verify-true
}

// verify the edit dialog appears when clcking the edit button
get-window "Preferences (Filtered)" | get-window "Host Connections" | get-group "Saved Credentials" 
    | get-button "Edit..." | click
get-window "Preferences (Filtered)" | get-window "Host Connections" | get-window "Edit Credentials" 
    | get-label [format "Credentials for cw01:%s" $PORT] | get-property caption | equals [format "Credentials for cw01:%s" $PORT] | verify-true

// remove the credentials and verify that it worked
with [get-window "Preferences (Filtered)" | get-window "Host Connections"] {
    get-window "Edit Credentials" | get-button Cancel | click
    get-group "Saved Credentials" | get-button "Remove All" | click
    get-window Credentials | get-button Yes | click
}
get-window "Preferences (Filtered)" | get-window "Host Connections" | get-group "Saved Credentials" | get-table 
    | get-property itemCount | equals 0 | verify-true
with [get-window "Preferences (Filtered)"] {
    get-window "Host Connections" | get-button OK | click
    get-button OK | click
}

// remove the default host
remove-host
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
