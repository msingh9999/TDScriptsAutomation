--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _PprFMER9EeamKrg04Gqe-Q,_wdXLMCqfEeeBwLfWkzSZ3Q,_l8aZYER7EeamKrg04Gqe-Q
Element-Name: ConfigureButton
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _QtCtcK3KEearMpD0xmwacQ
Runtime-Version: 2.2.0.201704030805
Save-Time: 7/24/17 4:27 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

log "---------------------------------------------- ConfigureButton ----------------------------------------------"

// add more than one host so that the host selection dialog will appear
add-host -hostName $HOST -portNum $PORT_20947
add-host -hostName $HOST -portNum $PORT_16196

// open the host selection dialog from the JES Explorer view
get-view "JES Explorer" | get-button "Connect...\tF6" | click
get-window "Host Explorer" | get-button "Configure..." | click

// verify that the correct preference node is selected and that the CES preference page link exists on the page and is enabled
with [get-window "Host Explorer" | get-window "Preferences (Filtered)"]
{
    get-tree | get-item "Compuware/Host Connections" | get-property selected | equals true | verify-true
    with [get-link "See <a>'Enterprise Services'</a> to synchronize connections defined on Compuware Enterprise Services (CES)."]
    {
        get-property className | equals "org.eclipse.swt.widgets.Link" | verify-true
        get-property enablement | equals true | verify-true
    }
}

// add a new host connection
with [get-window "Host Explorer" | get-window "Preferences (Filtered)"]
{
    get-button "Add..." | click
	with [get-window "Host Connections"]
	{
    	get-editbox -after [get-label "Host:"] | set-text $HOST
    	get-editbox -after [get-label "Port:"] | set-text $PORT
    	get-button OK | click
	}
	get-button OK | click
}

// verify that the newly added host connection is now selected in the host selection dialog
with [get-window "Host Explorer"]
{
    get-table | get-item [format "cw01:%s" $PORT] | get-property selected | equals true | verify-true
    get-button OK | get-property enablement | equals true | verify-true
}

// close the host selection dialog
get-window "Host Explorer" | get-button Cancel | click

// remove additional hosts
remove-host $HOST -portNum $PORT_16196
remove-host $HOST -portNum $PORT_20947
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
