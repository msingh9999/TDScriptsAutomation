--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _PprFMER9EeamKrg04Gqe-Q,_wvcyALzIEearCfJIpjdCZQ,_l8aZYER7EeamKrg04Gqe-Q
Element-Name: ConnectUsingCredentials
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _gBWFYL4aEearCfJIpjdCZQ
Runtime-Version: 2.2.0.201706152316
Save-Time: 9/20/17 8:07 AM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

/****** Global variables ******/

global [val exampleDirectory "Host Services API/Hosts/Connect Using Credentials"]
global [val resultDialogTitle "Connect Using Credentials Results"]

/****** Procedures ******/

proc "enter-credentials-in-dialog" [val userId "xdevreg"] [val password "regress"] [val groupId ""]
{
	with [get-window "Connect Using Credentials Example"]
	{
	    with [get-editbox -after [get-label "User ID:"]] 
	    {
	        set-text $userId
	    }
	    with [get-editbox -after [get-label "Password:"]] 
	    {
	        set-text $password
	    }
	    with [get-editbox -after [get-label "Group ID:"]] 
	    {
	        set-text $groupId
	    }
	    get-button Run | click
	}
}

/****** Start up ******/

open-example-launcher-view

/****** Tests ******/

// Test with no hosts defined
execute-example -exampleDirectory $exampleDirectory
verify-example-result -dialogTitle $resultDialogTitle -resultValue "There are no z/OS hosts defined.\n\nPlease define at least one z/OS host before running this example."

add-host

// Test with invalid group ID
execute-example -exampleDirectory $exampleDirectory
enter-credentials-in-dialog -groupId "blah"
verify-example-result -dialogTitle $resultDialogTitle -resultValue "User security could not be established on the HCI. The specified user ID is not valid for the group."

/** TODO: Temporarily removed because this was locking out the XDEVREG ID.
// log in and out of host to reduce chance of locking user ID
login-from-host
logout

// Test with invalid password
execute-example -exampleDirectory $exampleDirectory
enter-credentials-in-dialog -password "regresx"
verify-example-result -dialogTitle $resultDialogTitle -resultValue "User security could not be established on the HCI. The specified password is not correct."

// log in and out of host to reduce chance of locking user ID
login-from-host
logout
*/

// Test with invalid user ID
execute-example -exampleDirectory $exampleDirectory
enter-credentials-in-dialog -userId "abcdefg"
verify-example-result -dialogTitle $resultDialogTitle -resultValue "User security could not be established on the HCI. The specified user ID is not recognized."

// Test with valid credentials
execute-example -exampleDirectory $exampleDirectory
enter-credentials-in-dialog
verify-example-result -dialogTitle $resultDialogTitle -resultValue [format "Successfully connected to cw01:%s." $PORT]

// Test with multiple hosts
add-host -portNum $PORT_16196
execute-example -exampleDirectory $exampleDirectory
choose-host-in-dialog -hostPort "cw01:16196"
enter-credentials-in-dialog
verify-example-result -dialogTitle $resultDialogTitle -resultValue "Successfully connected to cw01:16196."

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
