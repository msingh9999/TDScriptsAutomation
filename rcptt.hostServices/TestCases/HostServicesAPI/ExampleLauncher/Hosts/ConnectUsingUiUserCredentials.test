--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _PprFMER9EeamKrg04Gqe-Q,_wvcyALzIEearCfJIpjdCZQ,_l8aZYER7EeamKrg04Gqe-Q
Element-Name: ConnectUsingUiUserCredentials
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _r9NdoL4eEearCfJIpjdCZQ
Runtime-Version: 2.2.0.201706152316
Save-Time: 9/20/17 8:28 AM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

/****** Global variables ******/

global [val exampleDirectory "Host Services API/Hosts/Connect Using UI User Credentials"]
global [val resultDialogTitle "Connect Using UI User Credentials Results"]

/****** Procedures ******/

proc "verify-login-dialog" [val resultValue]
{
	with [get-window "Login"]
	{
		get-editbox -after [get-label [format "Login Credentials for %s:%s" $HOST $PORT]] | get-property text | equals $resultValue | verify-true
	}
}

/**
 * Logs in using the Login dialog.
 * <p>
 * This procedure fails, if the Login dialog is not shown, so this procedure should be used instead of
 * handle-login-dialog when you want to verify the dialog is actually displayed when expected.
 */
proc "respond-to-login-dialog" [val userId "xdevreg"] [val password "regress"] [val groupId ""]
{
	with [get-window Login]
	{
    	get-editbox -after [get-label "User ID:"] | set-text $userId
		get-editbox -after [get-label "Password:"] | set-text $password
		get-editbox -after [get-label "Group ID:"] | set-text $groupId
    	get-button OK | click
	}
}

/****** Start up ******/

open-example-launcher-view

/****** Tests ******/

// Test with no hosts defined
execute-example -exampleDirectory $exampleDirectory
verify-example-result -dialogTitle $resultDialogTitle -resultValue "There are no z/OS hosts defined.\n\nPlease define at least one z/OS host before running this example."

add-host

// Test with invalid group ID
execute-example -exampleDirectory $exampleDirectory
respond-to-login-dialog -groupId "blah"
verify-login-dialog -resultValue "User security could not be established on the HCI. The specified user ID is not valid for the group."
get-window "Login" | get-button Cancel | click
verify-example-result -dialogTitle $resultDialogTitle -resultValue [format "User canceled login to %s:%s." $HOST $PORT]

/** TODO: Temporarily removed because this was locking out the XDEVREG ID.
// log in and out of host to reduce chance of locking user ID
login-from-host
logout

// Test with invalid password
execute-example -exampleDirectory $exampleDirectory
respond-to-login-dialog -password "regresx"
verify-login-dialog -resultValue "User security could not be established on the HCI. The specified password is not correct."
get-window "Login" | get-button Cancel | click
verify-example-result -dialogTitle $resultDialogTitle -resultValue [format "User canceled login to %s:%s." $HOST $PORT]

// log in and out of host to reduce chance of locking user ID
login-from-host
logout
*/

// Test with invalid user ID
execute-example -exampleDirectory $exampleDirectory
respond-to-login-dialog -userId "abcdefg"
verify-login-dialog -resultValue "User security could not be established on the HCI. The specified user ID is not recognized."
get-window "Login" | get-button Cancel | click
verify-example-result -dialogTitle $resultDialogTitle -resultValue [format "User canceled login to %s:%s." $HOST $PORT]

// Test with valid credentials
execute-example -exampleDirectory $exampleDirectory
respond-to-login-dialog
verify-example-result -dialogTitle $resultDialogTitle -resultValue [format "Successfully connected to %s:%s." $HOST $PORT]

// Test that the credentials prompt does not appear when already logged in
execute-example -exampleDirectory $exampleDirectory
verify-example-result -dialogTitle $resultDialogTitle -resultValue [format "Successfully connected to %s:%s." $HOST $PORT]

// Test with multiple hosts
add-host -portNum $PORT_16196
execute-example -exampleDirectory $exampleDirectory
choose-host-in-dialog -hostPort "cw01:16196"
respond-to-login-dialog
verify-example-result -dialogTitle $resultDialogTitle -resultValue "Successfully connected to cw01:16196."

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
