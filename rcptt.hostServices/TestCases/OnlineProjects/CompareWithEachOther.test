--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _XnPvkEUIEeamKrg04Gqe-Q,_PprFMER9EeamKrg04Gqe-Q,_kRuZMER7EeamKrg04Gqe-Q,_gJI9gIl3Eeaf8rtXp2pZyA
Element-Name: CompareWithEachOther
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _zg4lYEUHEeamKrg04Gqe-Q
Runtime-Version: 2.2.0.201706152316
Save-Time: 9/27/17 6:35 AM
Testcase-Type: ecl

------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa
Content-Type: text/plain
Entry-Name: .description

Test Online Projects "Compare With Each Other"
	- Disabled menu
			- Single selection
			- More than 2 selected
			- Folder Selection

	- Compare 
			- 2 PDS memebrs
			- 2 Sequential files
			- 1 PDS member and 1 sequential file

	- Compare & Edit
			- Compare Editor reflects changes from another editor
			- Compare editor does not lock a host resource

------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa--
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

log "---------------------------------------------- CompareWithEachOther ----------------------------------------------"

let [val projectName OnlineProject] {
	create-online-project $projectName

	add-source-online-project -projectName $projectName
		-datasetName [format "%s\n%s\n%s\n%s" $XDEVREG.COMPARE $XDEVREG.SEQ.COMPARE1 $XDEVREG.SEQ.COMPARE2 $XDEVREG.SEQ.COMPARE3]

	// test disbaled "compare with|each other" context menu for non file selection
	get-view $PROJECT_EXPLORER | get-tree | select [get-project-dataset-path -project $projectName -dataset $XDEVREG.COMPARE]
	get-view $PROJECT_EXPLORER | get-tree | get-menu "Compare With/Each Other" | get-property enablement | equals false | verify-true

	// test disabled "compare with|each other" context for folder and file selection
	get-view $PROJECT_EXPLORER | get-tree 
		| select [get-project-dataset-path -project $projectName -dataset $XDEVREG.COMPARE]
		     [get-project-pds-member-path -project $projectName -dataset $XDEVREG.COMPARE -member "COMPARE1.data"]
	get-view $PROJECT_EXPLORER | get-tree | get-menu "Compare With/Each Other" | get-property enablement | equals false | verify-true

	// test disabled "compare with|each other" context for 3 files
	get-view $PROJECT_EXPLORER | get-tree
		| select [get-project-pds-member-path -project $projectName -dataset $XDEVREG.COMPARE -member "COMPARE1.data"]
		     [get-project-pds-member-path -project $projectName -dataset $XDEVREG.COMPARE -member "COMPARE2.data"]
		     [get-project-pds-member-path -project $projectName -dataset $XDEVREG.COMPARE -member "COMPARE3.data"]
	get-view $PROJECT_EXPLORER | get-tree | get-menu "Compare With/Each Other" | get-property enablement | equals false | verify-true

	// compare 2 identical PDS members
	get-tab-folder | get-tab-item $PROJECT_EXPLORER | click
	with [get-view $PROJECT_EXPLORER | get-tree] {
		select [get-project-pds-member-path -project $projectName -dataset $XDEVREG.COMPARE -member "COMPARE1.data"]
			   [get-project-pds-member-path -project $projectName -dataset $XDEVREG.COMPARE -member "COMPARE2.data"]
		get-menu "Compare With/Each Other" | click
	}
	
	get-tab-folder -index 1 | get-property activePage | equals "Compare COMPARE1.data - COMPARE2.data" | verify-true
	get-editor "Compare COMPARE1.data - COMPARE2.data" | get-button "Next Difference" -index 1 
		| get-property enablement | equals false | verify-true
	
	with [get-editor "Compare COMPARE1.data - COMPARE2.data"] 
	{
	    get-label [format "%s(COMPARE1) on %s:%s" $XDEVREG.COMPARE $HOST $PORT] | assert-non-empty
	    get-label [format "%s(COMPARE2) on %s:%s" $XDEVREG.COMPARE $HOST $PORT] | assert-non-empty
	    close
	}

	// compare 2 identical sequential files
	with [get-view $PROJECT_EXPLORER | get-tree] {
		select [get-project-dataset-path -project $projectName -dataset [format "%s.data" $XDEVREG.SEQ.COMPARE1]]
		       [get-project-dataset-path -project $projectName -dataset [format "%s.data" $XDEVREG.SEQ.COMPARE2]]
		get-menu "Compare With/Each Other" | click
	}
	get-tab-folder -index 1 | get-property activePage | equals [format "Compare %s.data - %s.data" $XDEVREG.SEQ.COMPARE1 $XDEVREG.SEQ.COMPARE2] | verify-true

	get-editor "Compare XDEVREG.SEQ.COMPARE1.data - XDEVREG.SEQ.COMPARE2.data" | get-button "Next Difference" -index 1
		| get-property enablement | equals false | verify-true

	get-editor "Compare XDEVREG.SEQ.COMPARE1.data - XDEVREG.SEQ.COMPARE2.data" | close

	// Compare 1 PDS member and 1 sequential file
	with [get-view $PROJECT_EXPLORER | get-tree] {
		select [get-project-pds-member-path -project $projectName -dataset $XDEVREG.COMPARE -member "COMPARE4.data"]
		       [get-project-dataset-path -project $projectName -dataset [format "%s.data" $XDEVREG.SEQ.COMPARE3]]

		get-menu "Compare With/Each Other" | click
	}

	get-tab-folder -index 1 | get-property activePage | equals [format "Compare COMPARE4.data - %s.data" $XDEVREG.SEQ.COMPARE3]
		| verify-true

	get-editor [format "Compare COMPARE4.data - %s.data" $XDEVREG.SEQ.COMPARE3] | get-button "Next Difference" -index 1
		| get-property enablement | equals false | verify-true

	get-editor [format "Compare COMPARE4.data - %s.data" $XDEVREG.SEQ.COMPARE3] | close

	// Compare editor gets updated after a file is edited in another compuware supported editor
	with [get-view $PROJECT_EXPLORER | get-tree] {
		select [get-project-pds-member-path -project $projectName -dataset $XDEVREG.COMPARE -member "COMPARE1.data"]
			   [get-project-pds-member-path -project $projectName -dataset $XDEVREG.COMPARE -member "COMPARE2.data"]	
		get-menu "Compare With/Each Other" | click
		
		select [get-project-pds-member-path -project $projectName -dataset $XDEVREG.COMPARE -member "COMPARE1.data"] 
			| get-menu 	"Open With/Compuware Editor" | click
	}
	
	with [get-editor "COMPARE1.data" | get-text-viewer] 
	{
		set-caret-pos 2 6
		type-text x
	}
	
	get-window "Read-only File Encountered" | get-button Yes | click
	get-editor "COMPARE1.data" | get-text-viewer | type-text xx
	get-button "Save (M1+S)" | click
	get-tab-folder -index 1 | get-tab-item "Compare COMPARE1.data - COMPARE2.data" | click
	get-editor "Compare COMPARE1.data - COMPARE2.data" | get-button "Next Difference" -index 1 | get-property enablement
		| equals true | verify-true
	get-tab-folder -index 1 | get-tab-item "COMPARE1.data" | click
	with [get-editor "COMPARE1.data" | get-text-viewer] 
	{
		set-caret-pos 2 9
		key-type BackSpace -times 3
	}
	get-button "Save (M1+S)" | click

	// check for the lock decorator = true
	has-image-decorator -view $PROJECT_EXPLORER -decorName $IN_USE_DECORATOR 
		-path [get-project-pds-member-path -project $projectName -dataset $XDEVREG.COMPARE -member "COMPARE1.data"] 
		| verify-true

	get-tab-folder -index 1 | get-tab-item "Compare COMPARE1.data - COMPARE2.data" | click
	get-editor "Compare COMPARE1.data - COMPARE2.data" | get-button "Next Difference" -index 1 | get-property enablement
		| equals false | verify-true
	get-editor "Compare COMPARE1.data - COMPARE2.data" | close

	get-editor "COMPARE1.data" | close

	// check for the lock decorator = false
	has-image-decorator -view $PROJECT_EXPLORER -decorName $IN_USE_DECORATOR -path [get-project-pds-member-path
		-project $projectName -dataset $XDEVREG.COMPARE -member "COMPARE1.data"] | verify-false
		
	with [get-view $PROJECT_EXPLORER | get-tree] {
		select [get-project-pds-member-path -project $projectName -dataset $XDEVREG.COMPARE -member "COMPARE1.data"]
			   [get-project-pds-member-path -project $projectName -dataset $XDEVREG.COMPARE -member "COMPARE2.data"]
		get-menu "Compare With/Each Other" | click
	}

	// check for the lock decorator = false
	has-image-decorator -view $PROJECT_EXPLORER -decorName $IN_USE_DECORATOR 
		-path [get-project-pds-member-path -project $projectName -dataset $XDEVREG.COMPARE -member "COMPARE1.data"] 
		| verify-false

	get-editor "Compare COMPARE1.data - COMPARE2.data" | close
	
	//open previously edited file from Host Explorer and check lock decorator
	add-filter $FILTER_XDEVREG
	
	with [get-view $HOST_EXPLORER | get-tree] 
	{
    	select [get-pds-member-path -filter $FILTER_XDEVREG -dataset $XDEVREG.COMPARE -member "COMPARE1"] [get-pds-member-path -filter $FILTER_XDEVREG -dataset $XDEVREG.COMPARE -member "COMPARE2"]
    	get-menu "Compare With/Each Other" | click
	}
	
	// There is a rare issue where reopening a resource previously editied from an Online project will re-lock the
	// resource. Commenting this assertion out until we can fix that issue (this may be a while, since it is rare. You
	// can most relyably reproduce this bye executing this test immediately after restarting the AUT)
    /*has-image-decorator -view $HOST_EXPLORER -decorName $IN_USE_DECORATOR 
    	-path [get-pds-member-path -filter $FILTER_XDEVREG -dataset $XDEVREG.COMPARE -member "COMPARE1"] 
		| verify-false*/
    
    has-image-decorator -view $HOST_EXPLORER -decorName $IN_USE_DECORATOR 
    	-path [get-pds-member-path -filter $FILTER_XDEVREG -dataset $XDEVREG.COMPARE -member "COMPARE2"] 
		| verify-false

	get-editor "Compare COMPARE1.data - COMPARE2.data" | close
	
	//Open Compare, Open Editor, Close compare, verify editor is still open
	with [get-view $PROJECT_EXPLORER | get-tree] {
		select [get-project-pds-member-path -project $projectName -dataset $XDEVREG.COMPARE -member "COMPARE1.data"]
			   [get-project-pds-member-path -project $projectName -dataset $XDEVREG.COMPARE -member "COMPARE2.data"]
		get-menu "Compare With/Each Other" | click
		
		select [get-project-pds-member-path -project $projectName -dataset $XDEVREG.COMPARE -member "COMPARE1.data"] | double-click
	}
	
	get-tab-folder -index 1 | get-tab-item "Compare COMPARE1.data - COMPARE2.data" | click
	get-editor "Compare COMPARE1.data - COMPARE2.data" | close
	
	get-tab-folder -index 1 | get-property activePage | equals "COMPARE1.data" | verify-true
	get-editor "COMPARE1.data" | close
	
	delete-online-project $projectName
}

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
