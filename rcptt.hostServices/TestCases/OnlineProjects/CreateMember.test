--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _XnPvkEUIEeamKrg04Gqe-Q,_PprFMER9EeamKrg04Gqe-Q,_l8_BIER7EeamKrg04Gqe-Q
Element-Name: CreateMember
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _zhBIQEUHEeamKrg04Gqe-Q
Runtime-Version: 2.3.0.201712150019
Save-Time: 4/9/18 10:02 AM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

//PROCS
proc "create-member" [val folder] [val memberName] [val datasetName] [val projectName]
{
	get-view "Project Explorer" | get-tree | select [format "%s [%s:%s]/%s/%s [%s:%s]" $projectName $HOST $PORT $folder $datasetName $HOST $PORT] 
    | get-menu "New/PDS Member File in a Compuware Mainframe Project" | click
    
	with [get-window "New PDS Member File"]
	{
	    get-editbox -after [get-label "Member name:"] | set-text $memberName
	    get-button Finish | click
	}
	
	//Verify PDS member was automatically opened in an editor and close it
	get-editor [format "%s.data" $memberName] | close
	
	//Verify new member was added to the appropiate folder (Source or Concatenation List)
	get-view "Project Explorer" | get-tree 
    | get-item [format "%s [%s:%s]/%s/%s [%s:%s]/%s.data [%s on %s:%s]" $projectName $HOST $PORT $folder $datasetName $HOST $PORT $memberName $datasetName $HOST $PORT]
}

proc "test-enablement" [val folder] [val datasetName] [val projectName]
{
	get-view "Project Explorer" | get-tree | select [format "%s [%s:%s]/%s/%s [%s:%s]" $projectName $HOST $PORT $folder $datasetName $HOST $PORT] 
    | get-menu "New/PDS Member File in a Compuware Mainframe Project" | click
    
    with [get-window "New PDS Member File"]
    {
	    //Verify PDS was automatically selected
	    get-tree | get-item [format "%s [%s:%s]/%s/%s [%s:%s]" $projectName $HOST $PORT $folder $datasetName $HOST $PORT]
	    | get-property selected | equals true | verify-true
	    
	    //Verify 'Member name:' enablement is disabled when clicking on 'Source'
	    get-tree | select [format "%s [%s:%s]/Source" $projectName $HOST $PORT]
		get-editbox -after [get-label "Member name:"] | get-property readOnly | equals true | verify-true
		
		//Verify 'Member name:' enablement is disabled when clicking on 'Copybook Concatenation'
	    get-tree | select [format "%s [%s:%s]/Copybook Concatenation" $projectName $HOST $PORT]
		get-editbox -after [get-label "Member name:"] | get-property readOnly | equals true | verify-true
		
		//Verify 'Member name:' enablement is disabled when clicking on project node
	    get-tree | select [format "%s [%s:%s]" $projectName $HOST $PORT]
		get-editbox -after [get-label "Member name:"] | get-property readOnly | equals true | verify-true
		
	    get-button Cancel | click
	}
}

proc "test-login-prompt" [val projectName] [val folder] [val datasetName]
{	
	get-view "Project Explorer" | get-tree | select [format "%s [%s:%s]/%s/%s [%s:%s]" $projectName $HOST $PORT $folder $datasetName $HOST $PORT] 
    	| get-menu "New/PDS Member File in a Compuware Mainframe Project" | click
    	
	with [get-window "New PDS Member File"]
	{
    	get-editbox -after [get-label "Member name:"] | set-text "LOGIN"
    	get-button Finish | click
    	with [get-window Login]
    	{
       		get-editbox -after [get-label "User ID:"] | set-text xdevreg 
        	get-editbox -after [get-label "Password:"] | set-text [decrypt "GCCQ5KeLMRHd4spOzx8A1g=="]
        	get-button OK | click
        }
    }
    
    get-editor "LOGIN.data" | close
}

add-host -hostName $HOST -portNum $PORT
login-from-host
create-online-project -projectName "TestOnlineProject" -hostName $HOST -port $PORT

add-filter -filter $FILTER_XDEVREG -hostName $HOST -portNum $PORT

global [val pdsName [format "XDEVREG.RCPTT.TEMP.T%s" [get-time | format-time -format "mmssSSS"]]]
create-pds -hostName $HOST -portNum $PORT -filerName $FILTER_XDEVREG -pdsName $pdsName

add-source-online-project -hostName $HOST -portNum $PORT -projectName "TestOnlineProject" -datasetName $pdsName
test-enablement -folder "Source" -datasetName $pdsName -projectName "TestOnlineProject"

logout -hostName $HOST -port $PORT
test-login-prompt -folder "Source" -projectName "TestOnlineProject" -datasetName $pdsName

create-member -folder "Source" -memberName "TEST1" -datasetName $pdsName -projectName "TestOnlineProject"

PE-has-image-decorator [get-view "Project Explorer" | get-tree 
    | get-item [format "TestOnlineProject \\[%s:%s\\]/Source/%s \\[%s:%s\\]" $HOST $PORT $pdsName $HOST $PORT]] "com.compuware.frameworks.imagerepository/images/ovr/linkDecorator.gif" | equals true
    
remove-source -folder "Source" -datasetName $pdsName -projectName "TestOnlineProject" -hostName $HOST -portNum $PORT

add-copybook-online-project -project "TestOnlineProject" -hostName $HOST -portNum $PORT -copybook $pdsName
create-member -folder "Copybook Concatenation" -memberName "TEST2" -datasetName $pdsName -projectName "TestOnlineProject"

PE-has-image-decorator [get-view "Project Explorer" | get-tree 
    | get-item [format "TestOnlineProject \\[%s:%s\\]/Copybook Concatenation/%s \\[%s:%s\\]" $HOST $PORT $pdsName $HOST $PORT]] "com.compuware.frameworks.imagerepository/images/ovr/linkDecorator.gif" | equals true

remove-copybook -datasetName $pdsName -projectName "TestOnlineProject" -hostName $HOST -portNum $PORT

delete-online-project -projectName "TestOnlineProject" -hostName $HOST -port $PORT

delete-pds -hostName $HOST -portNum $PORT -filter $FILTER_XDEVREG -datasetName $pdsName

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
