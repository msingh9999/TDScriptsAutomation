--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _UrIe0ER7EeamKrg04Gqe-Q,_YuwFQER7EeamKrg04Gqe-Q,_eEaF4ER8EeamKrg04Gqe-Q,_ktRQQER8EeamKrg04Gqe-Q,_urTgQEU4EeamKrg04Gqe-Q,_PprFMER9EeamKrg04Gqe-Q,_l8_BIER7EeamKrg04Gqe-Q,_kRuZMER7EeamKrg04Gqe-Q,_DIm2ALsyEeem0fyxENOpag
Element-Name: CreateReportRequestCheatsheets
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _oN3VAMliEeeuRPE7E3vB_w
Runtime-Version: 2.2.0.201706152316
Save-Time: 11/17/17 8:55 AM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

proc "expand-section" [val sectionName]
{
	if [get-view "Cheat Sheets" | get-section $sectionName | get-link | get-property "isExpanded()" -raw | equals false]
	{
		get-view "Cheat Sheets" | get-section $sectionName | click
	}
}

try -command
{
	close-view "Cheat Sheets"
}
-catch
{
}

// Open the Code Coverage "Create Report Request" cheat sheet
// Open Code Coverage Report Request cheat sheet
get-menu -path "Help/Cheat Sheets..." | click
get-window "Cheat Sheet Selection" | get-tree 
    | select "Compuware/Code Coverage\\/Eclipse/Creating and Running a Code Coverage Report Request" | double-click

// Start the cheat sheet over if already completed.
expand-section Introduction
with [get-view "Cheat Sheets" | get-section Introduction]
{
	get-link | get-property "isExpanded()" | equals true | verify-true
    get-canvas | get-property text | contains "running Code Coverage report requests" | verify-true
	try -command
	{
	    get-link "Click to Restart" | click
	}
	-catch
	{
	    get-link "Click to Begin" | click
	}
}

with [get-view "Cheat Sheets" | get-section "Open the Code Coverage Perspective"]
{
	get-link | get-property "isExpanded()" | equals true | verify-true
    get-canvas | get-property text | contains "select Compuware > Code Coverage" | verify-true
}
// Verify cheat sheet step is accurate:
get-menu -path "Compuware/Code Coverage" | get-property enablement | equals true | verify-true
get-view "Cheat Sheets" | get-section "Open the Code Coverage Perspective" | get-link "Click when complete" | click

with [get-view "Cheat Sheets" | get-section "Add an existing Repository"]
{
	get-link | get-property "isExpanded()" | equals true | verify-true
    get-canvas | get-property text | contains "click the Add... menu item" | verify-true
}
// Verify cheat sheet step is accurate:
get-view "Code Coverage" | get-tree | select Repositories | get-menu -path "Add..." | click
with [get-window "Code Coverage"]
{
    get-combo -after [get-label "Host:"] | get-property selection | equals [format "%s:%s" $HOST $PORT] | verify-true
    get-button "Configure..." | get-property enablement | equals true | verify-true
    with [get-section Repositories]
    {
        get-editbox -after [get-label "Name:"] | set-text XDEVREG
        get-button List | click
        get-table | select "XDEVREG.CC.REPOSIT"
    }
    get-button OK | click
}
get-view "Cheat Sheets" | get-section "Add an existing Repository" | get-link "Click when complete" | click

with [get-view "Cheat Sheets" | get-section "Create a Code Coverage Report Request"]
{
	get-link | get-property "isExpanded()" | equals true | verify-true
    get-canvas | get-property text | contains "the Create a New Report Request button can be used" | verify-true
}
// Verify cheat sheet step is accurate:
with [get-view "Code Coverage" | get-button "Create a New Report Request"]
 {
    get-property enablement | equals true | verify-true
    get-property tooltip | equals "Create a New Report Request" | verify-true
}
get-view "Cheat Sheets" | get-section "Create a Code Coverage Report Request" | get-link "Click to perform" | click

with [get-window "Code Coverage Report Request" | get-section "Create a Code Coverage Report Request"]
{
	get-link | get-property "isExpanded()" | equals true | verify-true
    get-canvas | get-property text | contains "the Create a New Report Request button can be used" | verify-true
    get-link "Click when complete" | click
}

with [get-window "Code Coverage Report Request" | get-section "Name the Report"]
{
	get-link | get-property "isExpanded()" | equals true | verify-true
    get-canvas | get-property text | contains "enter a name" | verify-true
}
// Verify cheat sheet step is accurate:
with [get-window "Code Coverage Report Request"]
{
    get-editbox -after [get-label "Report request name:"] | set-text "A Test Report Request"
    get-section "Name the Report" | get-link "Click when complete" | click
}

with [get-window "Code Coverage Report Request" | get-section "Enter the Report Criteria"]
{
	get-link | get-property "isExpanded()" | equals true | verify-true
    get-canvas | get-property text | contains "Specify names section" | verify-true
    get-canvas | get-property text | contains "Specify a list" | verify-true
}
// Verify cheat sheet step is accurate:
with [get-window "Code Coverage Report Request"]
{
	get-button "Specify a list" | click
    // The cheat sheet doesn't specifiy what types of lists you can use, so just test the control types.
    with [get-combo -after [get-label "Type:"]]
    {
        get-property className | equals "org.eclipse.swt.widgets.Combo" | verify-true
        get-property enablement | equals true | verify-true
    }
    with [get-table]
    {
        get-property className | equals "org.eclipse.swt.widgets.Table" | verify-true
        get-property enablement | equals true | verify-true
    }

    get-button "Specify names"  | click
    // The cheat sheet doesn't specifiy what names you can enter, so just test the first one after "Specify names".
    get-editbox -after [get-button "Specify names"] | set-text "Test Filter"

    get-section "Enter the Report Criteria" | get-link "Click when complete" | click
}

with [get-window "Code Coverage Report Request" | get-section "Enter the Filters"]
{
	get-link | get-property "isExpanded()" | equals true | verify-true
    get-canvas | get-property text | contains "Add Filter Criteria dialog" | verify-true
}
// Verify cheat sheet step is accurate:
with [get-window "Code Coverage Report Request"]
{
    get-tab-folder | get-tab-item Filters | click
    get-button "New..." | click
	get-window "Code Coverage Filter" | get-label "Create Filter" | get-property caption | equals "Create Filter"
		| verify-true
	with [get-window "Code Coverage Filter"]
	{
	    get-editbox -after [get-label "Name:"] | set-text "Test Filter"
	    get-button "Add..." | click
		get-window "Code Coverage Filter Criteria" | get-label "Add Filter Criteria" | get-property caption 
		    | equals "Add Filter Criteria" | verify-true
	    with [get-window "Code Coverage Filter Criteria"]
	    {
	        get-group Masking | get-editbox -after [get-label "Begin mask:"] | set-text "TEST MASK"
	        get-button OK | click
	    }
	    get-button Cancel | click
	}

    get-section "Enter the Filters" | get-link "Click when complete" | click
}

with [get-window "Code Coverage Report Request" | get-section "Enter the Options"]
{
	get-link | get-property "isExpanded()" | equals true | verify-true
    get-canvas | get-property text | contains "Combine altered debug sessions" | verify-true
    get-canvas | get-property text | contains "Run to save and run" | verify-true
}
// Verify cheat sheet step is accurate:
with [get-window "Code Coverage Report Request"]
{
    get-tab-folder | get-tab-item Options | click
	with [get-button "Combine altered debug sessions with unaltered sessions"]
	{
	    get-property enablement | equals true | verify-true
	    get-property caption | equals "Combine altered debug sessions with unaltered sessions" | verify-true
	}
    get-button Run | get-property enablement | equals true | verify-true
    get-button OK | get-property enablement | equals true | verify-true
	get-button Cancel | click
}

with [get-view "Cheat Sheets" | get-section "Enter the Options"]
{
	get-link | get-property "isExpanded()" | equals true | verify-true
    get-canvas | get-property text | contains "Combine altered debug sessions" | verify-true
    get-canvas | get-property text | contains "Run to save and run" | verify-true
	get-link "Click when complete" | click
}

with [get-view "Cheat Sheets" | get-section "Creating and Running a Code Coverage Report Request Complete"]
{
	get-link | get-property "isExpanded()" | equals true | verify-true
    get-canvas | get-property text | contains "The Code Coverage report request is now complete" | verify-true
	get-link "Click to perform" | click
}
// Verify new cheat sheet is opened
get-view "Cheat Sheets" | get-label "Viewing and Printing Code Coverage Statistics" | get-property caption 
    | equals "Viewing and Printing Code Coverage Statistics" | verify-true

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
