--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _UrIe0ER7EeamKrg04Gqe-Q,_YuwFQER7EeamKrg04Gqe-Q,_eEaF4ER8EeamKrg04Gqe-Q,_ktRQQER8EeamKrg04Gqe-Q,_rRRHUMuhEeeMV-oFXOCR1A,_urTgQEU4EeamKrg04Gqe-Q,_PprFMER9EeamKrg04Gqe-Q,_l8_BIER7EeamKrg04Gqe-Q,_kRuZMER7EeamKrg04Gqe-Q,_DIm2ALsyEeem0fyxENOpag,_R8eCQMuhEeeMV-oFXOCR1A
Element-Name: ViewPrintStatisticsCheatsheet
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _LjHa4MueEeeMV-oFXOCR1A
Runtime-Version: 2.2.0.201706152316
Save-Time: 11/17/17 1:50 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

proc "expand-section" [val sectionName]
{
	if [get-view "Cheat Sheets" | get-section $sectionName | get-link | get-property "isExpanded()" -raw | equals false]
	{
		get-view "Cheat Sheets" | get-section $sectionName | click
	}
}

try -command
{
	close-view "Cheat Sheets"
}
-catch
{
}

// Open the Code Coverage "Viewing and Printing Statistics" cheat sheet
get-menu -path "Help/Cheat Sheets..." | click
get-window "Cheat Sheet Selection" | get-tree 
    | select "Compuware/Code Coverage\\/Eclipse/Viewing and Printing Code Coverage Statistics" | double-click

// Start the cheat sheet over if already completed.
expand-section Introduction
with [get-view "Cheat Sheets" | get-section Introduction]
{
	get-link | get-property "isExpanded()" | equals true | verify-true
    get-canvas | get-property text | contains "viewing Code Coverage statistics" | verify-true
	try -command
	{
	    get-link "Click to Restart" | click
	}
	-catch
	{
	    get-link "Click to Begin" | click
	}
}

with [get-view "Cheat Sheets" | get-section "Open the Code Coverage Perspective"]
{
	get-link | get-property "isExpanded()" | equals true | verify-true
    get-canvas | get-property text | contains "select Compuware > Code Coverage" | verify-true
}
// Verify cheat sheet step is accurate:
get-menu -path "Compuware/Code Coverage" | get-property enablement | equals true | verify-true
get-view "Cheat Sheets" | get-section "Open the Code Coverage Perspective" | get-link "Click when complete" | click

with [get-view "Cheat Sheets" | get-section "Run a Code Coverage Report Request"]
{
	get-link | get-property "isExpanded()" | equals true | verify-true
    get-canvas | get-property text | contains "select Run or double-click on a report request" | verify-true
}
// Verify cheat sheet step is accurate:
get-view "Code Coverage" | get-tree | select [format "Report Requests/%s" $TEST_REPORT] | get-menu -path Run | click
get-view "Cheat Sheets" | get-section "Run a Code Coverage Report Request" | get-link "Click when complete" | click

with [get-view "Cheat Sheets" | get-section "View the Summary Statistics"]
{
	get-link | get-property "isExpanded()" | equals true | verify-true
    get-canvas | get-property text | contains "show the execution statistics" | verify-true
	get-link "Click when complete" | click
}
// This section is not specific enough to verify

with [get-view "Cheat Sheets" | get-section "View Annotated Listing"]
{
	get-link | get-property "isExpanded()" | equals true | verify-true
    get-canvas | get-property text | contains "select View Annotated Listing" | verify-true
}
// Verify cheat sheet step is accurate:
with [get-view "Test Report Request" | get-tree]
{
	select "System: CW01/Load Module: CWXTCOB/CWXTCOB \\(2017-09-12 09:08:22\\)"
	get-menu -path "View Annotated Listing" | get-property enablement | equals true | verify-true
	select "System: CW01/Load Module: CWXTCOB/CWXTCOB \\(2017-09-12 09:08:22\\)" | double-click
}
has-editor "CWXTCOB (2017-09-12 090822).cbl" | verify-true
get-view "Cheat Sheets" | get-section "View Annotated Listing" | get-link "Click when complete" | click

with [get-view "Cheat Sheets" | get-section "Annotations View"]
{
	get-link | get-property "isExpanded()" | equals true | verify-true
    get-canvas | get-property text | contains "The Annotations view displays" | verify-true
}
// Verify cheat sheet step is accurate:
get-menu -path "Window/Show View/Annotations" | click
get-view Annotations | get-tree | select "Unexecuted \\(112 items\\)/Unexecuted" | double-click
// Make sure the line is positioned past the procedure division (since the sort order of the annotations view changes,
// it is hard to know the exect line here.)
get-editor "CWXTCOB (2017-09-12 090822).cbl" | get-text-viewer | get-property "caretPosition.line" -raw | gt 344 
    | verify-true
get-editor "CWXTCOB (2017-09-12 090822).cbl" | close
get-view "Cheat Sheets" | get-section "Annotations View" | get-link "Click when complete" | click

with [get-view "Cheat Sheets" | get-section "Print Annotated Listing"]
{
	get-link | get-property "isExpanded()" | equals true | verify-true
    get-canvas | get-property text | contains "Printable Report Options dialog" | verify-true
}
// Verify cheat sheet step is accurate:
get-view "Test Report Request" | get-tree | select "System: CW01/Load Module: CWXTCOB/CWXTCOB (2017-09-12 09:08:22)" 
    | get-menu -path "View Printer-Friendly Annotated Listing..." | click
get-window "Printer-Friendly Annotated Listing Options" | get-button OK | click
has-editor "CWXTCOB (2017-09-12 090822).txt" | verify-true
get-menu -path "File/Print..." | get-property enablement | equals true | verify-true
get-editor "CWXTCOB (2017-09-12 090822).txt" | close
get-view "Cheat Sheets" | get-section "Print Annotated Listing" | get-link "Click when complete" | click

with [get-view "Cheat Sheets" | get-section "View Statistics Complete"]
{
	get-link | get-property "isExpanded()" | equals true | verify-true
    get-canvas | get-property text | contains "Viewing statistics is now complete" | verify-true
	get-link "Click when complete" | click
}
// Verify Introduction is now expanded
get-view "Cheat Sheets" | get-section Introduction | get-link | get-property "isExpanded()" | equals true | verify-true

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
